#!/usr/bin/env coffee

fs   = require "fs"
exec = require("child_process").exec

# 該当箇所の前後でプレビューする行数
previewLines = 3

# エスケープシーケンス列挙
color = ['black', 'red', 'green', 'yellow', 'blue', 'purple', 'cyan', 'gray']

escapeByCode = (str) -> "\x1b[#{str}m"

bg = color.reduce (prev, curr, index) ->
  colorCode = index + 40
  prev[curr] = escapeByCode colorCode
  prev
, {}

fg = color.reduce (prev, curr, index) ->
  colorCode = index + 30
  prev[curr] = escapeByCode colorCode
  prev
, {}

cls = escapeByCode '0'
bold = escapeByCode '1'
underline = escapeByCode '4'
inverse = escapeByCode '7'

# 指定されたファイルを読み込み不要な空白をチェックする
checkWhitespace = (filename, callback) ->
  lineEndRegex   = new RegExp(/\s+$/)
  fileEndRegex   = new RegExp(/\s{2,}$/)
  whitespaceFlag = false

  fs.readFile filename, "utf8", (err, fileContent) ->
    if err?
      console.log "\n#{filename} を開く際にエラーが発生しました"
      throw err

    if fileContent.match fileEndRegex
      whitespaceFlag = true
      console.log "\n#{bg.purple}#{filename} の終わりに余分な空白があります#{cls}"

    # ファイルの中身を行ごとに分割
    lines = fileContent.split("\n")
    for lineCount in [0...lines.length]
      whitespaceIndex = lines[lineCount].search lineEndRegex
      if whitespaceIndex != -1
        whitespaceFlag = true
        console.log "\n#{bg.blue}#{filename} の #{lineCount+1} 行目の終わりに余分な空白があります#{cls}"

        # 該当行の前後をプレビュー
        # プレビューする行範囲を算出
        previewStart = lineCount - previewLines
        if previewStart < 0
          previewStart = 0
        previewEnd   = lineCount + previewLines
        if previewEnd > lines.length - 1
          previewEnd = lines.length - 1

        # プレビュー範囲を出力
        for previewLineCount in [previewStart..previewEnd]
          # 行番号をフォーマット
          formattedLineCount = (previewLineCount + 1).toString()
          while formattedLineCount.length < 5
            formattedLineCount = " " + formattedLineCount
          if lineCount == previewLineCount
            before = lines[previewLineCount].substr 0, whitespaceIndex
            after = lines[previewLineCount].substr whitespaceIndex
            console.log "#{inverse}#{formattedLineCount} #{cls}#{before}#{underline}#{inverse}#{after}#{cls}"
          else
            console.log "#{inverse}#{formattedLineCount} #{cls}#{lines[previewLineCount]}"
    callback whitespaceFlag

exec "git diff --cached --name-only", (err, stdoutFromGit, stderr) ->
  # git diff からの標準出力を受け取ってファイル名に分割
  files = stdoutFromGit.split "\n"
  # 空白のファイル名を除去
  for i in [0...files.length]
    if not files[i]
      files.splice i, 1

  # ファイルの処理が非同期で実行されてしまうので、終了時を確認するために必要な関数
  whitespaceFlag = false
  fileCount = 0
  collectResults = (flag) ->
    fileCount += 1
    if flag
      whitespaceFlag = true
    if fileCount == files.length and whitespaceFlag == true
      console.log ''
      console.log "#{bg.red}余分な空白があるためコミットを中止しました#{cls}"
      process.exit 1

  # ファイルの空白をチェック
  for filename in files
    if filename
      checkWhitespace filename, collectResults